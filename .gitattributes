;******************************************************************
;Universidad del Valle de Guatemala
;IE2023 Programación de Microcontroladores
;Autor: Fernando Donis
;Compilador: PIC-AS (v2.36), MPLAB X IDE (v6.00)
;Proyecto: Proyecto 1
;Hardware: PIC16F887
;Creado: 24/08/2022
;Última Modificación: 19/09/2022
;******************************************************************
PROCESSOR 16F887
#include <xc.inc>
    
;**************************************************************
;Palabra de configuración
;**************************************************************
; CONFIG1
  CONFIG  FOSC = INTRC_NOCLKOUT 
  CONFIG  WDTE = OFF            
  CONFIG  PWRTE = OFF            
  CONFIG  MCLRE = OFF           
  CONFIG  CP = OFF              
  CONFIG  CPD = OFF             
  CONFIG  BOREN = OFF           
  CONFIG  IESO = OFF            
  CONFIG  FCMEN = OFF           
  CONFIG  LVP = OFF              

; CONFIG2
  CONFIG  BOR4V = BOR40V        
  CONFIG  WRT = OFF             

MODO EQU 0
CONF  EQU 1
UP    EQU 2
DOWN  EQU 3
INIC  EQU 4
;***************************************************************************************************************************************************************************************
;Macros
;******************************************************************************************************************************
  RESET_TMR0 MACRO TMR_VAR
    BANKSEL TMR0	    ;cambiamos de banco
    MOVLW   TMR_VAR
    MOVWF   TMR0	    ;configuramos tiempo de retardo
    BCF	    T0IF	    ;limpiamos bandera de interrupción
    ENDM
  RESET_TMR1 MACRO TMR1_H, TMR1_L
    MOVLW   TMR1_H
    MOVWF   TMR1H	    ;50ms retardo
    MOVLW   TMR1_L	    ;limpiamos bandera de interrupción
    MOVWF   TMR1L
    BCF	    TMR1IF
    ENDM
;***************************************************************************************************************************************************************************************
;Variables
;******************************************************************************************************************************
PSECT udata_shr		    	;Memoria compartida
    W_TEMP:		DS 1
    STATUS_TEMP:	DS 1
PSECT udata_bank0	    	;banco de variables
    segundos:      DS 1	    ;Variable incrementada con el tmr1
    minutos:	   DS 1	    
    horas:	   DS 1
    ALARM:	   DS 2
    save:	   DS 2
    comp:	   DS 2
    dat:	   DS 3	    	;Copia para hacer las verificaciones
    bandera:	   DS 4	    ;bandera
    UNIDAD:	   DS 3
    DECENA:	   DS 3
    display:	   DS 4
    subs:	   DS 2
    subsALAR:	   DS 2
    subsFCH:	   DS 2
    funciones:	   DS 3
    indicador:	   DS 2
    nocero:	   DS 1
    FECHA:	   DS 3
PSECT resVect, class=CODE, abs, delta=2
ORG 00h			    		 ;posición 0000h para el reset
;***************************************************************************************************************************************************************************************
;Vector reset
;******************************************************************************************************************************
resetVec:
    PAGESEL MAIN	    	 ;Cambio de pagina
    GOTO    MAIN
    
PSECT intVect, class=CODE, abs, delta=2
ORG 04h			    ;posición 0004h para interrupciones
;***************************************************************************************************************************************************************************************
;Vector de interrupción
;******************************************************************************************************************************
PUSH:
    MOVWF   W_TEMP	    	;Guardamos W
    SWAPF   STATUS, W
    MOVWF   STATUS_TEMP	    ;Guardamos STATUS
    
ISR:
    BTFSC   T0IF
    CALL    int_tmr0
    BTFSC   TMR1IF
    CALL    AUMENTO
    BTFSC   TMR2IF
    CALL    leds
    BTFSC   RBIF
    CALL    INTB
    
    
POP:
    SWAPF   STATUS_TEMP, W  
    MOVWF   STATUS	    	;Recuperamos el valor de reg STATUS
    SWAPF   W_TEMP, F	    
    SWAPF   W_TEMP, W	    ;Recuperamos valor de W
    RETFIE		    		;Regresamos a ciclo principal

PSECT code, delta=2, abs
ORG 100h		    		;posición 100h para el codigo
;***************************************************************************************************************************************************************************************
;Configuración
;******************************************************************************************************************************   
MAIN:
    CALL    CONFIGIO	    ;Configuración de I/O
    CALL    CONFIGREL    	;Configuración de Oscilador
    CALL    CONFIGTMR0
    CALL    CONFIGTMR1	    ;Configuración de TMR0
    CALL    CONFIGTMR2
    CALL    CONFIGINT	    ;Configuración de interrupciones
    call    IOC
    BANKSEL PORTD	    	;Cambio a banco 00
    
LOOP:
    CLRF dat	    			;comienza en 0 siempre
    CLRF dat+1
    CLRF dat+2
    
    BTFSC   funciones, 0
    call    bancoRELOJ
    BTFSC   funciones, 1
    call    bancoALARM
    BTFSC   funciones, 2
    call    bancoFECHA
    
    call 	decenas				;separa decenas	
    call 	decenas2
    call 	decenas3
    call 	ready
    GOTO    LOOP	    
    
;***************************************************************************************************************************************************************************************
;Subrutinas
;******************************************************************************************************************************

ready:
    movf    UNIDAD, w
    call    Tabla
    movwf   display
    movf    DECENA, w
    call    Tabla
    movwf   display+1
    
    movf    UNIDAD+1, w
    call    Tabla
    movwf   display+2
    movf    DECENA+1, w
    call    Tabla
    movwf   display+3
    
    return
    
CONFIGREL:
    BANKSEL OSCCON	    	;cambiamos a banco 1
    BSF	    OSCCON, 0	    ;SCS -> 1, Usamos reloj interno
    BCF	    OSCCON, 6
    BSF	    OSCCON, 5
    BSF	    OSCCON, 4	    ;IRCF<2:0> -> 101 2MHz
    RETURN
    
CONFIGTMR0:		    ;retraso 50ms
    BANKSEL OPTION_REG	    ;cambiamos de banco
    BCF	    PSA		    	;prescaler a TMR0
    BSF	    PS2
    BSF	    PS1
    BSF	    PS0		    	;PS<2:0> -> 111 prescaler 1 : 256
    
    BANKSEL TMR0	     	;cambiamos de banco
    MOVLW   254
    MOVWF   TMR0	    	;2ms retardo
    BCF	    T0IF	    	;limpiamos bandera de interrupción
    RETURN
    
CONFIGTMR1:
    BANKSEL T1CON	    	;cambiamos de banco
    BCF	    TMR1GE	    	;TMR1 Siempre cuenta
    BSF	    T1CKPS1	    	;prescaler a TMR1
    BSF	    T1CKPS0	    	;PS<2:0> -> 11 prescaler 1 : 8
    BCF	    T1OSCEN	    	;OSC tmr1 desactivado
    BCF	    TMR1CS
    BSF	    TMR1ON
    
    RESET_TMR1 0xC2, 0xF7
    RETURN 
    
CONFIGTMR2:
    BANKSEL PR2
    MOVLW   244
    MOVWF   PR2				;500ms retardo
    BANKSEL T2CON       	;cambiamos de banco
    BSF	    T2CKPS1     	;prescaler a TMR2
    BSF	    T2CKPS0     	;PS<1:0> -> 1x prescaler 1 : 16
    
    BSF	    TOUTPS3     	;TMR2 postscaler 
    BSF	    TOUTPS2
    BSF	    TOUTPS1
    BSF	    TOUTPS0     	;PS<3:0> 1111 postescaler 1:16
    BSF	    TMR2ON      	;Enciende el TMR2
    
    
    RETURN

    
CONFIGIO:
    BANKSEL ANSEL
    CLRF    ANSEL
    CLRF    ANSELH	        ;I/O digitales
    BANKSEL TRISD
    CLRF    TRISC
    BCF     TRISD, 0
    BCF     TRISD, 1
    BCF     TRISD, 2
    BCF     TRISD, 3
    BCF     TRISA, 0
    BCF     TRISA, 1
    BCF     TRISA, 2
    BCF     TRISA, 3
    BCF     TRISA, 4 
    BCF     TRISA, 5
    
;***************************************************************************************************************************************************************************************
;Entradas
;******************************************************************************************************************************
    
    BSF TRISB, MODO
    BSF TRISB, CONF
    BSF TRISB, UP
    BSF TRISB, DOWN
    BSF TRISB, INIC
    BCF OPTION_REG, 7		;WPUB, PULL-UP
    BSF WPUB, MODO
    BSF WPUB, CONF
    BSF WPUB, UP
    BSF WPUB, DOWN
    BSF WPUB, INIC
    
    BANKSEL PORTD
    CLRF    PORTC
    BCF	    PORTD, 0
    BCF	    PORTD, 1
    BCF	    PORTD, 2
    BCF	    PORTD, 3
    BCF     PORTA, 0
    BSF     PORTA, 1
    BCF     PORTA, 2
    BCF     PORTA, 3
    BCF     PORTA, 4
    BCF	    PORTA, 5
    
    CLRF ALARM
    BCF ALARM, 1
    BCF ALARM, 0
    CLRF bandera
    CLRF FECHA
    BCF FECHA, 0
    BCF FECHA, 1
    BCF FECHA, 2
    MOVLW 0
    MOVWF  FECHA+0
    MOVLW 0
    MOVWF  FECHA+1
    MOVLW 0
    MOVWF  FECHA+2
    MOVWF  ALARM+0
    MOVWF  ALARM+1
    RETURN
    
CONFIGINT:
    BANKSEL PIE1 
    BSF	    TMR1IE
    BSF	    TMR2IE
    BANKSEL INTCON
    BSF	    PEIE	    	;Habilitamos interrupciones
    BSF	    GIE		    	;Habilitamos interrupcion TMR0
    BSF	    T0IE	    	;Habilitamos interrupcion TMR0
    BCF	    T0IF	    	;Limpiamos bandera de TMR0
    BCF	    TMR1IF
    BCF	    TMR2IF
    
 
    BSF RBIE
    BCF RBIF
    
    RETURN

    IOC:
	banksel TRISA
	BSF IOCB, MODO
	BSF IOCB, CONF
	BSF IOCB, UP
	BSF IOCB, DOWN
	BSF IOCB, INIC
	return    
    
;***************************************************************************************************************************************************************************************
;Reloj
;******************************************************************************************************************************
MINS:
	clrf segundos
	incf minutos
	return
HORS:
	clrf minutos
	incf horas
	return

;***************************************************************************************************************************************************************************************
;Logistica
;******************************************************************************************************************************
decenas:
	clrf	DECENA	    	;limpiar la variable donde se guardan las decenas	
	movlw	10	    		;mover 10 a w
	subwf	dat, W	    	;restar 10 al valor del PORT A
	btfsc	STATUS, 0   	;skip if el carry esta en 0
	incf	DECENA	    	;incrementar el contador de la variable decenas
	btfsc	STATUS, 0   	;skip if el carry esta en 0
	movwf	dat	    		; mover el valor de la resta a w
	btfsc	STATUS, 0   	;skip if el carry esta en 0
	goto	$-7	    		; si se puede seguir restando 10 entonces realizar todo el proceso
	goto	unidades	    ; si ya no se puede restar 10, por que la bandera de carry se encendio entonces ir a unidades
	return
	
unidades:
	clrf	UNIDAD	    	;limpiar la variable donde se guardan las unidades
	movlw	1	    		;mover 1 a w
	subwf	dat, F    		;restar 1 al valor del PORT A
	btfsc	STATUS, 0   	;skip if el carry esta en 0
	incf	UNIDAD	    	;incrementar el contador de la variable unidades
	btfss	STATUS, 0   	;si tenemos un carry en el valor entonces realizar otra vez el proceso
	return		    		;si no se puede seguir restando 1 erntonces se regresa al stack 
	goto $-6
	
decenas2:
	clrf	DECENA+1	    	
	movlw	10	    
	subwf	dat+1, W    
	btfsc	STATUS, 0   
	incf	DECENA+1	
	btfsc	STATUS, 0  
	movwf	dat+1	    
	btfsc	STATUS, 0   
	goto	$-7	   
	goto	unidades2	   
	return
	
unidades2:
	clrf	UNIDAD+1	    
	movlw	1	    
	subwf	dat+1, F    
	btfsc	STATUS, 0   
	incf	UNIDAD+1	    
	btfss	STATUS, 0   
	return		    
	goto $-6
	
decenas3:
	clrf	DECENA+2	    	
	movlw	10	    
	subwf	dat+2, W    
	btfsc	STATUS, 0   
	incf	DECENA+2	
	btfsc	STATUS, 0  
	movwf	dat+2	    
	btfsc	STATUS, 0   
	goto	$-7	   
	goto	unidades3	   
	return
	
unidades3:
	clrf	UNIDAD+2	    
	movlw	1	    
	subwf	dat+2, F    
	btfsc	STATUS, 0   
	incf	UNIDAD+2	    
	btfss	STATUS, 0   
	return		    
	goto $-6
	
leds:
	BCF	TMR2IF
	MOVLW   0x01
	XORWF   PORTA
	BTFSC   indicador, 0
	call    cfindic
	return
	
OVERFLOWS:
	MOVLW 60
	SUBWF segundos, w
	btfsc ZERO
	goto MINS
	RETURN
OVERFLOWM:
	MOVLW 60
	SUBWF minutos,  w
	btfsc ZERO
	goto HORS
	RETURN
OVERFLOWH:
	MOVLW 24
	SUBWF horas, w
	BTFSC ZERO
	BCF horas, 0
	RETURN
UNDERFLOWS:
	MOVLW 59
	MOVWF	segundos
	RETURN
UNDERFLOWM:
	MOVLW 59
	MOVWF	minutos
	RETURN
UNDERFLOWH:
	MOVLW 23
	MOVWF	horas
	RETURN
	
OVERFLOWST:
	MOVLW 60
	SUBWF ALARM, w
	btfsc ZERO
	CLRF  ALARM
	RETURN
UNDERFLOWST:
	MOVLW	59
	MOVWF	ALARM
	RETURN
OVERFLOWMT:
	MOVLW 23
	SUBWF ALARM+1, w
	btfsc ZERO
	CLRF  ALARM+1
	RETURN
UNDERFLOWMT:
	MOVLW	59
	MOVWF	ALARM+1
	RETURN

UNDERFLOWFD:
	MOVLW	31
	MOVWF	FECHA+0
	RETURN
;***************************************************************************************************************************************************************************************
;Almacena valores
;****************************************************************************************************************************** 
bancoRELOJ:
	MOVF    segundos,    w
	MOVWF   dat+2
	MOVF    minutos,    w
	MOVWF   dat	
	MOVF    horas,    w
	MOVWF   dat+1	
	return
    
bancoALARM:
	MOVF    ALARM+0, w
	MOVWF   dat
	MOVF    ALARM+1,  w
	MOVWF   dat+1
	return
    
bancoFECHA:
	MOVF    FECHA+1, w
	MOVWF   dat
	MOVF    FECHA+0,  w
	MOVWF   dat+1
	MOVF    FECHA+2,  w
	MOVWF   dat+2
	return
;***************************************************************************************************************************************************************************************
;Subrutinas extras
;******************************************************************************************************************************    
cfindic:
	movlw 0x10
	XORWF PORTA
	return
	
compalarm:
	BTFSS	comp, 1
	BTFSS	comp+1, 1
	RETURN
	
	MOVF	save, w
	SUBWF	dat, 0
	BTFSS	STATUS, 2
	RETURN
	
	MOVF	save+1, w
	SUBWF	dat, 0
	BTFSS	STATUS, 2
	RETURN
	
	BSF	PORTA, 5
	BCF	comp, 1
	BCF	comp+1, 1
	BSF	comp, 2
	BSF	comp+1, 2
	
	return
	
pulalarm:
	BTFSS	comp, 2
	BTFSS	comp+1, 2
	RETURN
	
	BTFSC	PORTA, 5
	GOTO	esdown
	RETURN
	BSF	PORTA, 5
	RETURN
	
    esdown:
	BCF PORTA, 5
	RETURN
	
offalarm:
	BTFSS	comp, 2
	RETURN
	
	BTFSC	comp, 0
	GOTO	off
	
	MOVF	save, w
	SUBLW	1
	BTFSS	STATUS, 2
	RETURN
	
off:
	BCF comp, 2
	BCF PORTA, 5
	RETURN
;***************************************************************************************************************************************************************************************
;Estados
;******************************************************************************************************************************
    
INTB:				
    BTFSC PORTA, 2
    GOTO ESTADO1
    BTFSC PORTA, 3
    GOTO ESTADO2
    BTFSC subs,	 0
    GOTO confMINS
    BTFSC subs,	 1
    GOTO confHORS
    BTFSC subsALAR, 0
    GOTO minALAR
    BTFSC subsALAR, 1
    GOTO horALAR
    BTFSC subsFCH, 0
    GOTO monFCH
    BTFSC subsFCH, 1
    GOTO dayFHC
    
    ESTADO0:					;Estado del reloj
		BCF	funciones, 2
		BSF	funciones, 0
		BCF	PORTA, 4
		BCF	indicador, 0
		BSF	PORTA, 1
		BTFSS	PORTB, MODO
		call	flag0
		BTFSS	PORTB, CONF
		call	mflags
		BCF	RBIF
		RETURN
	
    ESTADO1:					;Estado de la alarma
		BCF	PORTA, 4
		BCF	indicador, 0
    
		BTFSS	PORTB, MODO
		call	flag1
		BCF	PORTA, 1
		BCF	funciones, 0	
		BSF	funciones, 1
	
		BTFSS	PORTB, INIC
		call	compalarm
		call	pulalarm
		call    offalarm
		BTFSS	PORTB, CONF
		call	tsflag
		BCF	RBIF
		RETURN
    
    ESTADO2:					;Estado de la fecha
		BCF	PORTA, 4
		BCF	indicador, 0
	
		BTFSS	PORTB, MODO
		CALL	flag2
		BCF	funciones, 1
		BSF	funciones, 2
	
		BTFSS	PORTB, CONF
		call	mond_flag
		BCF	RBIF
		RETURN
;***************************************************************************************************************************************************************************************
;Subconfiguracion del reloj
;******************************************************************************************************************************
confMINS:
	BTFSS	PORTB, CONF
	call	hflags
	BTFSS	PORTB, INIC
	BCF	subs, 0
	BTFSS	PORTB, UP
	INCF	minutos
	    call    OVERFLOWM 
	BTFSS	PORTB, DOWN
	DECF	minutos
	    MOVLW   255
	    SUBWF   minutos, W
	    BTFSC   ZERO
	    CALL    UNDERFLOWM
	    BSF	indicador, 0
	    BCF	RBIF
	RETURN
	
confHORS:
	BTFSS	PORTB, CONF
	call	rflags
	BTFSS	PORTB, INIC
	BCF	subs, 1
	BTFSS	PORTB, UP
	INCF	horas
	    MOVLW 24
	    subwf   horas, w
	    BTFSC ZERO
	    CLRF horas
	BTFSS	PORTB, DOWN
	DECF	horas
	    MOVLW   255
	    SUBWF   horas, w
	    BTFSC   ZERO
	    call    UNDERFLOWH
	 BCF	RBIF
	RETURN
;***************************************************************************************************************************************************************************************
;Subconfiguracion del Alarma
;******************************************************************************************************************************
minALAR:
	BTFSS	PORTB, CONF
	call	tmflag
	BTFSS	PORTB, INIC
	call	tcflag
	
	BTFSS	PORTB, UP
	INCF	ALARM
	    call    OVERFLOWST
	BTFSS	PORTB, DOWN
	DECF	ALARM
	    MOVLW   255
	    SUBWF   ALARM, W
	    BTFSC   ZERO
	    call    UNDERFLOWST
	movf	ALARM, W
	movwf	save
	bsf	comp, 1
	BSF	indicador, 0
	BCF	RBIF
	RETURN
	
horALAR:
	BTFSS	PORTB, CONF
	call	tbflag
	BTFSS	PORTB, INIC
	call	tcflag
	
	BTFSS	PORTB, UP
	INCF	ALARM+1
	    call    OVERFLOWMT 
	BTFSS	PORTB, DOWN
	DECF	ALARM+1
	    MOVLW   255
	    SUBWF   ALARM+1, W
	    BTFSC   ZERO
	    call    UNDERFLOWMT
	movf	ALARM+1, W
	movwf	save+1
	bsf	comp+1, 1
	BCF	RBIF
	RETURN
    
;***************************************************************************************************************************************************************************************
;Subconfiguracion de la fecha
;******************************************************************************************************************************
monFCH:
	BTFSS	PORTB, CONF
	call	day_flag
	BTFSS	PORTB, INIC
	call	saved_flag
	
	BTFSS	PORTB, UP
	INCF	FECHA+1
	    MOVLW 13
	    subwf   FECHA+1, w
	    BTFSC ZERO
	    CLRF FECHA+1
	BTFSS	PORTB, DOWN
	DECF	FECHA+1
	BSF	indicador, 0
	BCF	RBIF
	RETURN
    
dayFHC:
	BTFSS	PORTB, CONF
	call	mond_flag
	BTFSS	PORTB, INIC
	call	saved_flag
	
	BTFSS	PORTB, UP
	INCF	FECHA+0
	BTFSS	PORTB, DOWN
	DECF	FECHA+0
	BCF	RBIF
	    MOVLW 32
	    subwf   FECHA+0, w
	    BTFSC ZERO
	    CLRF FECHA+0
	RETURN
    
;***************************************************************************************************************************************************************************************
;Bandera de los estados
;******************************************************************************************************************************
flag0:
	BCF PORTA, 1
	BSF PORTA, 2
    return
	
flag1:
	BCF PORTA, 2
	BSF PORTA, 3
    return
	
flag2:
	BCF PORTA, 3
    return
;***************************************************************************************************************************************************************************************
;Banderas de configuracion de reloj
;******************************************************************************************************************************
mflags:
	BCF PORTA, 1
	BSF subs, 0
    return
    hflags:
	BCF subs, 0
	BSF subs, 1
    return
	
rflags:
	BCF subs, 1
	BSF subs, 0
    return
;***************************************************************************************************************************************************************************************
;Banderas alarma
;******************************************************************************************************************************
tsflag:
	BCF PORTA, 2
	BSF subsALAR, 0
    return
	
tmflag:
	BCF subsALAR, 0
	BSF subsALAR, 1
    return
	
tbflag:
	BCF subsALAR, 1
	BSF subsALAR, 0
    return
    tcflag:
	BCF nocero, 0
	BCF subsALAR, 0
	BCF subsALAR, 1
	BSF PORTA,   2
	MOVF	ALARM+1, w
	BTFSC ZERO  
	CALL SEGU
	BTFSS nocero, 0
	BSF indicador, 1
    return
    
SEGU:
	MOVF ALARM+0, w
	BTFSC ZERO
	BSF nocero, 0
    RETURN
;***************************************************************************************************************************************************************************************
;Banderas de fecha
;******************************************************************************************************************************
mond_flag:
	BCF PORTA,	3
	BSF subsFCH,	0
    RETURN
    
day_flag:
	BCF subsFCH,	0
	BSF subsFCH,	1
    RETURN
    
return_flag:
	BCF subsFCH,	1
	BSF subsFCH,	0
    RETURN
    
    
saved_flag:
	BCF subsFCH,	1
	BCF subsFCH,	0
	BSF PORTA,	3
    RETURN
    
UNDERFLOWRELOJ:
	movlw 255
	subwf horas, w
	BTFSC ZERO
	call UNDERFLOWH
    return
    
;***************************************************************************************************************************************************************************************
;Incrementos
;******************************************************************************************************************************
    
AUMENTO:
    RESET_TMR1 0xC2, 0xF7
    INCF segundos
    CALL OVERFLOWS				;reinicio
    CALL OVERFLOWM
    MOVLW 24
    subwf   horas, w
    BTFSC ZERO
    CLRF horas
    RETURN
    
int_tmr0:
    RESET_TMR0 254
    clrf PORTD
    BTFSC   bandera, 0
    goto    disp2
    BTFSC   bandera, 1
    goto    disp3
    BTFSC   bandera, 2
    goto    disp4
    
disp1:
    movf    display, w
    movwf   PORTC
    BSF	    PORTD, 0
    BCF	    bandera, 3
    BSF	    bandera, 0
    return
disp2:
    movf    display+1, w
    movwf   PORTC
    BSF	    PORTD, 1
    BCF	    bandera, 0
    BSF	    bandera, 1
    return
disp3:
    movf    display+2, w
    movwf   PORTC
    BSF	    PORTD, 2
    BCF	    bandera, 1
    BSF	    bandera, 2
    return
disp4:
    movf   display+3, w
    movwf   PORTC
    BSF	    PORTD, 3
    BCF	    bandera, 2
    BSF	    bandera, 3
    return
	
ORG 200h    
;***************************************************************************************************************************************************************************************
;Tabla
;******************************************************************************************************************************
Tabla:			    
    CLRF PCLATH
    BSF PCLATH, 1
    ANDLW 0x0f
    ADDWF PCL 
    RETLW 00111111B ;0
    RETLW 00000110B ;1
    RETLW 01011011B ;2
    RETLW 01001111B ;3
    RETLW 01100110B ;4
    RETLW 01101101B ;5
    RETLW 01111101B ;6
    RETLW 00000111B ;7
    RETLW 01111111B ;8
    RETLW 01101111B ;9
    RETLW 00111111B ;0